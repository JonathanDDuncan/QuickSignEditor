
Expression
  = Sign

SignZoom =    ZoomPrefix zoom:Number
	{return zoom}

SymbolsZoom =    symbolzooms:(SymbolZoom *)
	{return symbolzooms}

SymbolZoom =
ZoomPrefix index:SymbolIndex "," zoom:Number
	{return {index : index, zoom: zoom}}

ZoomPrefix = "Z"

ColorizePrefix = "C"

Colorize =
 	colorize:ColorizePrefix
    { return colorize == "C"
    }

SignColors =  ColorPrefix color:Color
	{ return  {fore: color.fore,back: color.back};  }

SymbolsColors =   colors: SymbolColors *
	{ return   colors;  }

SymbolColors = ColorPrefix index:SymbolIndex color:Color
	{ return  {index: index, fore: color.fore,back: color.back};  }

SymbolIndex = digits: (digit digit)
		 { return parseInt(digits.join(""), 10); }

SignStylingPrefix
	= "-"

SymbolStylingPrefix
	= "--"

ColorPrefix = "D"

Color =  PairPrefix foregroundcolor: (RGB / ColorText) "," backgroundcolor: (RGB / ColorText) PairSuffix
	{ return  {fore: foregroundcolor, back: backgroundcolor};  }

RGB = hexa hexa hexa hexa hexa hexa {return text()}
ColorText =[a-wyzA-Z]+ {return text()}


PairPrefix = "_"
PairSuffix  = "_"

Document = SignSpacer *

SignSpacer =
Sign Space

Sign =  sort:Sorting ?  sign:UnSortedSign
	{ return  { sorting: sort, sign: sign } }
UnSortedSign = prefix:SignPrefix symbols:SymbolCoordinate * styling:Styling
	{ return  { l: prefix.lane,
    c: prefix.signcoordinate,
    styling : styling,
    symbols: symbols } }

Styling = symbols: SymbolsStylings  ? sign:SignStylings ?
	{ return { colorize:sign ? sign.colorize : null ,colors : sign ? sign.colors : null,
    symbolscolors : symbols ? symbols.symbolscolors :null,
    signzoom :  sign ? sign.signzoom : null,
    symbolszoom: symbols ? symbols.symbolszoom : null}}

SignStylings = SignStylingPrefix colorize:Colorize colors:SignColors ? signzoom:SignZoom ?
{ return { colorize:  colorize , colors : colors, signzoom : signzoom}}

SymbolsStylings = SymbolStylingPrefix  symbolscolors:SymbolsColors ? symbolszoom:SymbolsZoom ?
   { return {   symbolscolors : symbolscolors , symbolszoom: symbolszoom}}

SymbolCoordinate = symb:Symbol coord:Coordinate
	{ return { k: symb, c: coord

    } }

Sorting = "A" symbols: Symbol *
	{ return  symbols  }


Symbol =
	result: "S"[123]hexa hexa[0-5]hexa
   {return text()}

SignPrefix = lane:[BLMR] signcoordinate: Coordinate ?
	{return { lane: lane, signcoordinate:signcoordinate}}


Coordinate = x1:ThreeInts CoordinateSpacer y1:ThreeInts
  {

      return { x: x1, y : y1 };
    }

ThreeInts "threeints"
	 = digit digit digit  { return parseInt(text(), 10); }

Float "float"
  = digit + ( "." digit +) ? { return parseFloat(text(), 10); }

Integer "integer"
  = digit+ { return parseInt(text(), 10); }

Number "number" = Float / Integer

CoordinateSpacer "coordinatespacer"
  = [Xx]

digit = [0-9]
hexa = [0-9a-f]

Space "space"  = " " *

Whitespace "whitespace"
  = [ \t\n\r]*
